import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'

sourceCompatibility = 17

repositories {
	mavenCentral()
	//Paper
	maven {
		url 'https://repo.papermc.io/repository/maven-public/'
	}
	//Skript
	maven {
		url 'https://repo.destroystokyo.com/repository/maven-public/'
	}
	//ViaVersion
	maven {
		url 'https://repo.viaversion.com/'
	}
	//LandLord
	maven {
		url 'https://eldonexus.de/repository/maven-releases'
	}
	//SimpleClans
	maven {
		url 'https://repo.codemc.org/repository/maven-public'
	}
	//JitPack
	maven {
		url 'https://jitpack.io'
	}
	maven {
		url 'https://repo.skriptlang.org/releases'
	}
}
configurations {
	compileWithJar
}
dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	testImplementation 'com.github.seeseemelk:MockBukkit-v1.19:3.1.0'
	compileOnly 'org.projectlombok:lombok:1.18.38'
	annotationProcessor 'org.projectlombok:lombok:1.18.38'
	implementation 'org.jetbrains:annotations:26.0.1'
	//This is the default bukkit version supported. It is just for javadocs and jar hierarchy
	implementation 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
	implementation 'commons-lang:commons-lang:2.6'
	//Since it uses NMS/OBC stuffs, it's required to manually add the bukkit jars.
	implementation fileTree(dir: 'dependencies', include: '*.jar')
	//ProtocolSupport
	//implementation 'com.github.ProtocolSupport:ProtocolSupport:'
	//Skript
	implementation 'com.github.SkriptLang:Skript:2.9.5'
	//Generex API. Used to expressions to generate strings from regex
	compileWithJar 'com.github.mifmif:generex:1.0.2'
	//ViaVersion
	implementation 'com.viaversion:viaversion-api:4.0.0'
	//SimpleClans
	implementation 'net.sacredlabyrinth.phaed.simpleclans:SimpleClans:2.15.0'
	//LandLord
	implementation 'biz.princeps:landlord-api:4.350'
	//GameProfile
	compileOnly 'com.mojang:authlib:1.5.25'
	configurations.implementation.extendsFrom(configurations.compileWithJar)
}

//Used to copy the final jar in test server located in at same place
//It requires that you execute "./gradlew clean build copy"
task copy(type: Copy) {
	from 'build/libs/TuSKe.jar'
	into 'server/plugins'
}

processResources {
	filter ReplaceTokens, tokens: [
			'version': project.property('version')
	]
}

test {
	useJUnitPlatform()
}

jar {
	archiveFileName = 'TuSKe-'+project.property('version')+'-SZPatchX'+'.jar'
	from {
		configurations.compileWithJar.collect { it.isDirectory() ? it : zipTree(it) }
	}
}